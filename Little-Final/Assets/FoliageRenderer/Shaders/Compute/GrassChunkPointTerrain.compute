#pragma kernel InitializeGrassChunk

#include "Simplex.compute"
#include "GrassData.cginc"

// Dependencies
RWStructuredBuffer<GrassData> _GrassDataBuffer;
float4x4 _ObjectWorldMatrix;

// Terrain
Texture2D<float4> _HeightMap;
SamplerState sampler_HeightMap;
float _DisplacementStrength;

// Chunks
int _Dimension, _Scale, _XOffset, _YOffset, _NumChunks, _ChunkDimension;

[numthreads(8,8,1)]
void InitializeGrassChunk(uint3 id : SV_DispatchThreadID) {
    if (id.x < uint(_ChunkDimension) && id.y < uint(_ChunkDimension)) {
        GrassData grass;
        float4 pos = 0.0f;

        const float scale = float(_Scale);
        const float dimension = float(_Dimension);
        const float chunk_dimension = float(_ChunkDimension);

        pos.x = (id.x - (chunk_dimension * 0.5f * _NumChunks)) + chunk_dimension * _XOffset;
        pos.z = (id.y - (chunk_dimension * 0.5f * _NumChunks)) + chunk_dimension * _YOffset;
        pos.xz *= (1.0f / scale);

        pos.x += snoise(float3(pos.xz + _XOffset + _YOffset, 0.0f) * 3.0f) * 0.05f;
        pos.z += snoise(float3(pos.xz + _XOffset + _YOffset, 0.0f) * 4.0f) * 0.05f;

        float uv_x = pos.x + dimension * 0.5f * _NumChunks * (1.0f / _NumChunks);
        float uv_y = pos.z + dimension * 0.5f * _NumChunks * (1.0f / _NumChunks);

        float2 uv = float2(uv_x, uv_y) / dimension;
        uv.y = 1 - uv.y;
        uv.x = 1 - uv.x;

        float4 displacement = _HeightMap.SampleLevel(sampler_HeightMap, uv, 0);

        pos.y += displacement.r * _DisplacementStrength + 0.5f;

        const float noise = abs(snoise(float3(pos.xz + _XOffset + _YOffset, 0.0f) * 2.2f));

        pos.w = lerp(0.3f, 0.8f, noise);
        
        grass.position = pos;
        grass.displacement = displacement.r * _DisplacementStrength;
        grass.uv = uv;
        _GrassDataBuffer[id.x + id.y * _ChunkDimension] = grass;
    }
}